defmodule PhoenixKit.Users.MagicLinkRegistration do
  @moduledoc """
  Two-step user registration via Magic Link email.

  Flow:
  1. User enters email
  2. Receives magic link via email
  3. Clicks link to complete registration with profile details
  """

  import Ecto.Query, warn: false

  require Logger
  alias PhoenixKit.RepoHelper, as: Repo

  alias PhoenixKit.Config
  alias PhoenixKit.Settings
  alias PhoenixKit.Users.Auth
  alias PhoenixKit.Users.Auth.{User, UserToken}
  alias PhoenixKit.Users.RateLimiter
  alias PhoenixKit.Users.Referrals
  alias PhoenixKit.Utils.Routes

  @magic_link_registration_context "magic_link_registration"

  @doc """
  Sends a registration magic link to the specified email address.

  Rate-limited on the same buckets as password registration — this is a
  registration entry point, and it was the one public auth endpoint with no
  limit at all. `ip_address` is optional only because callers without peer data
  exist; pass it whenever you have it, or a single client can spread its
  attempts across unlimited addresses.
  """
  def send_registration_link(email, ip_address \\ nil) when is_binary(email) do
    email = String.trim(email) |> String.downcase()

    # BEFORE the lookup, deliberately. Limiting after it would throttle only
    # addresses that resolve to no user, which turns the identical
    # success/already-registered responses back into an existence oracle via
    # timing — the exact shape password reset had to be fixed for.
    with true <- valid_email?(email),
         :ok <- RateLimiter.check_registration_rate_limit(email, ip_address),
         nil <- Auth.get_user_by_email(email) do
      generate_and_send_token(email)
    else
      false -> {:error, :invalid_email}
      {:error, :rate_limit_exceeded} -> {:error, :rate_limit_exceeded}
      %User{} -> {:error, :email_already_exists}
    end
  end

  @doc """
  Verifies a magic link registration token.
  """
  def verify_registration_token(token) when is_binary(token) do
    case Base.url_decode64(token, padding: false) do
      {:ok, decoded_token} ->
        hashed_token = :crypto.hash(:sha256, decoded_token)
        expiry_minutes = get_expiry_minutes()

        query =
          from t in UserToken,
            where:
              t.token == ^hashed_token and
                t.context == ^@magic_link_registration_context and
                t.inserted_at > ago(^expiry_minutes, "minute"),
            select: t

        case Repo.one(query) do
          %UserToken{sent_to: email} = _token ->
            {:ok, email}

          nil ->
            {:error, :invalid_token}
        end

      :error ->
        {:error, :invalid_token}
    end
  end

  @doc """
  Completes user registration using a magic link token.
  """
  def complete_registration(token, attrs, ip_address \\ nil)
      when is_binary(token) and is_map(attrs) do
    case verify_registration_token(token) do
      {:ok, email} ->
        case Auth.get_user_by_email(email) do
          %User{} ->
            delete_registration_token(token)
            {:error, :email_already_exists}

          nil ->
            do_complete_registration(email, attrs, ip_address, token)
        end

      {:error, reason} ->
        {:error, reason}
    end
  end

  @doc """
  Deletes a registration token.
  """
  def delete_registration_token(token) when is_binary(token) do
    case Base.url_decode64(token, padding: false) do
      {:ok, decoded_token} ->
        hashed_token = :crypto.hash(:sha256, decoded_token)

        query =
          from t in UserToken,
            where: t.token == ^hashed_token and t.context == ^@magic_link_registration_context

        Repo.delete_all(query)
        :ok

      :error ->
        :ok
    end
  end

  @doc """
  Generates a registration magic link URL.
  """
  def registration_link_url(token) when is_binary(token) do
    Routes.url("/users/register/verify/#{token}")
  end

  @doc """
  Cleans up expired registration tokens.
  """
  def cleanup_expired_tokens do
    expiry_minutes = get_expiry_minutes()

    query =
      from t in UserToken,
        where:
          t.context == ^@magic_link_registration_context and
            t.inserted_at <= ago(^expiry_minutes, "minute")

    {deleted_count, _} = Repo.delete_all(query)
    deleted_count
  end

  # Private functions

  defp generate_and_send_token(email) do
    {token, user_token} =
      UserToken.build_email_token_for_context(email, @magic_link_registration_context)

    case Repo.insert(user_token) do
      {:ok, _} ->
        registration_url = registration_link_url(token)

        case send_registration_email(email, registration_url) do
          {:ok, _} ->
            {:ok, email, token}

          {:error, reason} ->
            delete_registration_token(token)
            {:error, reason}
        end

      {:error, changeset} ->
        {:error, changeset}
    end
  end

  defp do_complete_registration(email, attrs, ip_address, token) do
    {referral_code, attrs} = Map.pop(attrs, "referral_code")

    # `confirmed_at` is NOT settable here: `registration_changeset/3` casts a
    # fixed allowlist that omits it, so putting it in attrs was silently
    # dropped. The account is confirmed explicitly after insert instead.
    attrs = Map.put(attrs, "email", email)

    track_geolocation = Settings.get_boolean_setting("track_registration_geolocation", false)

    result =
      if track_geolocation && ip_address do
        Auth.register_user_with_geolocation(attrs, ip_address)
      else
        Auth.register_user(attrs, ip_address)
      end

    case result do
      {:ok, user} ->
        if referral_code do
          process_referral_code(user, referral_code)
        end

        delete_registration_token(token)

        # Clicking the emailed link already proved control of this inbox, so
        # the account is confirmed. Without this the user is auto-logged-in and
        # immediately parked at /users/confirm — and this flow never sends a
        # confirmation email, so there is no link to click and the only way out
        # is the resend form.
        {:ok, confirm_registered_user(user)}

      {:error, changeset} ->
        {:error, changeset}
    end
  end

  defp confirm_registered_user(user) do
    case Auth.admin_confirm_user(user) do
      {:ok, confirmed} ->
        confirmed

      {:error, reason} ->
        Logger.warning(
          "[PhoenixKit] magic-link registration could not confirm #{user.uuid}: #{inspect(reason)}"
        )

        user
    end
  end

  defp send_registration_email(email, registration_url) do
    temp_user = %{email: email}
    Auth.UserNotifier.deliver_magic_link_registration(temp_user, registration_url)
  end

  defp process_referral_code(user, referral_code) when is_binary(referral_code) do
    if Code.ensure_loaded?(Referrals) do
      Referrals.record_signup_use(user, referral_code)
    end

    :ok
  end

  defp valid_email?(email) when is_binary(email) do
    String.match?(email, ~r/^[^\s]+@[^\s]+\.[^\s]+$/)
  end

  def get_expiry_minutes do
    Config.get(:magic_link_for_registration_expiry_minutes, 30)
  end
end
